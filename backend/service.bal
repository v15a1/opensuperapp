// Copyright (c) 2025 WSO2 LLC. (https://www.wso2.com).
//
// WSO2 LLC. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

import superapp_mobile_service.authorization;
import superapp_mobile_service.database;
import superapp_mobile_service.entity;
import superapp_mobile_service.scim;
import superapp_mobile_service.wallet;

import ballerina/http;
import ballerina/log;

configurable int maxHeaderSize = 16384; // 16KB header size for WSO2 Choreo support
configurable string[] regionRestrictedMicroApps = ?;
configurable string userRegionFilter = ?; // Region to bypass region restricted micro-apps
configurable string mobileAppReviewerEmail = ?; // App store reviewer email
configurable MicroAppScope[] appScopes = []; // Additional scopes required for micro-apps

const int MAX_ITEMS_PER_PAGE = 1000;

@display {
    label: "SuperApp Mobile Service",
    id: "wso2-open-operations/superapp-mobile-service"
}
service class ErrorInterceptor {
    *http:ResponseErrorInterceptor;

    remote function interceptResponseError(error err, http:RequestContext ctx) returns http:BadRequest|error {
        if err is http:PayloadBindingError {
            string customError = "Payload binding failed!";
            log:printError(customError, err);
            return {
                body: {
                    message: customError
                }
            };
        }
        return err;
    }
}

service http:InterceptableService / on new http:Listener(9090, config = {requestLimits: {maxHeaderSize}}) {

    # + return - authorization:JwtInterceptor, ErrorInterceptor
    public function createInterceptors() returns http:Interceptor[] =>
        [new authorization:JwtInterceptor(), new ErrorInterceptor()];

    function init() returns error? {
        log:printInfo("Super app mobile backend started.");
    }

    # Fetch application configuration details for the given user groups and config key.
    #
    # + ctx - Request context
    # + return - `AppConfig` or `http:InternalServerError` if the operation fails.
    resource function get app\-configs(http:RequestContext ctx) returns AppConfig|http:InternalServerError {
        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        string[]|error defaultMicroAppIds = database:getMicroAppIdsByGroups([database:defaultMicroAppsGroup]);
        if defaultMicroAppIds is error {
            string customError = "Failed to fetch default micro app IDs";
            log:printError(customError, defaultMicroAppIds);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        database:AppConfig[]|error appConfigs = database:getAppConfigs();
        if appConfigs is error {
            string customError = "Error occurred while retrieving app settings!";
            log:printError(customError, appConfigs);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        log:printDebug("Fetching app configurations...", userId = userInfo.userId, configs = appConfigs,
                defaultMicroAppIds = defaultMicroAppIds, appScopes = appScopes);

        return <AppConfig>{
            appConfigs,
            defaultMicroAppIds,
            appScopes
        };
    }

    # Fetch user information of the logged in users.
    #
    # + ctx - Request context
    # + return - User information object or an error
    resource function get user\-info(http:RequestContext ctx)
        returns entity:Employee|http:InternalServerError|http:NotFound {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        entity:Employee|error? loggedInUser = getUserInfo(userInfo.email);
        if loggedInUser is error {
            string customError = "Error occurred while retrieving user data!";
            log:printError(customError, loggedInUser);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        if loggedInUser is () {
            log:printWarn("User not found!", email = userInfo.email);
            return http:NOT_FOUND;
        }

        error? cacheError = userInfoCache.put(userInfo.email, loggedInUser);
        if cacheError is error {
            log:printError("Error in updating the user cache!", cacheError);
        }

        return loggedInUser;
    }

    # Build the Apple Wallet business card pass of the logged in user.
    #
    # + ctx - Request context
    # + req - HTTP request, used to forward the caller's JWT assertion to the wallet service
    # + return - The `.pkpass` bytes, or an `http:InternalServerError` if the operation fails
    resource function get business\-card/pkpass(http:RequestContext ctx, http:Request req)
        returns http:Response|http:Unauthorized|http:BadGateway|http:InternalServerError {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        string|error userToken = authorization:getUserAccessToken(req);
        if userToken is error {
            string customError = "Missing invoker info header!";
            log:printError(customError, userToken);
            return <http:Unauthorized>{
                body: {
                    message: customError
                }
            };
        }

        log:printDebug("Building the Apple Wallet pass", userId = userInfo.userId);
        byte[]|wallet:WalletError pass =
            wallet:getApplePass(toWalletCardRequest(toBusinessCard(userInfo)), userToken);
        if pass is wallet:WalletError {
            return walletFailure("Apple Wallet pass", userInfo.userId, pass);
        }

        http:Response response = new;
        response.setBinaryPayload(pass, "application/vnd.apple.pkpass");
        response.setHeader("Content-Disposition", string `attachment; filename="wso2-business-card.pkpass"`);
        response.setHeader("Cache-Control", "no-store");
        return response;
    }

    # Build the Google Wallet save URL for the business card of the logged in user.
    #
    # + ctx - Request context
    # + req - HTTP request, used to forward the caller's JWT assertion to the wallet service
    # + return - The Google Wallet save URL, or an `http:InternalServerError` if the operation fails
    resource function get business\-card/google\-save\-url(http:RequestContext ctx, http:Request req)
        returns wallet:GoogleSaveUrl|http:Unauthorized|http:BadGateway|http:InternalServerError {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        string|error userToken = authorization:getUserAccessToken(req);
        if userToken is error {
            string customError = "Missing invoker info header!";
            log:printError(customError, userToken);
            return <http:Unauthorized>{
                body: {
                    message: customError
                }
            };
        }

        log:printDebug("Building the Google Wallet save URL", userId = userInfo.userId);
        wallet:GoogleSaveUrl|wallet:WalletError saveUrl =
            wallet:getGoogleSaveUrl(toWalletCardRequest(toBusinessCard(userInfo)), userToken);
        if saveUrl is wallet:WalletError {
            return walletFailure("Google Wallet save URL", userInfo.userId, saveUrl);
        }

        return saveUrl;
    }

    # Retrieves the list of micro apps available to the authenticated user.
    #
    # + ctx - Request context
    # + return - A list of microapps if successful, or an error on failure
    resource function get micro\-apps(http:RequestContext ctx) returns database:MicroApp[]|http:InternalServerError {
        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        database:MicroApp[]|error allMicroApps = database:getMicroApps(userInfo.groups ?: []);
        if allMicroApps is error {
            string customError = "Error occurred while retrieving Micro Apps!";
            log:printError(customError, err = allMicroApps.message());
            return {
                body: {
                    message: customError
                }
            };
        }

        // Bypass the filtering for the app store reviewer
        if userInfo.email == mobileAppReviewerEmail {
            return allMicroApps;
        }

        entity:Employee|error? loggedInUser = getUserInfo(userInfo.email);
        if loggedInUser is error {
            string customError = "Error occurred while retrieving user data!";
            log:printError(customError, loggedInUser);
            return {
                body: {
                    message: customError
                }
            };
        }

        database:MicroApp[] filteredMicroApps = allMicroApps;

        if loggedInUser is entity:Employee && loggedInUser.location != userRegionFilter {
            filteredMicroApps = allMicroApps.filter(microapp => regionRestrictedMicroApps.indexOf(microapp.appId) is ());
        }

        return filteredMicroApps;
    }

    # Retrieves details of a specific micro app based on its App ID.
    #
    # + ctx - Request context
    # + appId - ID of the micro app to retrieve
    # + return - Single microapp, or errors on failure and not found
    resource function get micro\-apps/[string appId](http:RequestContext ctx)
        returns database:MicroApp|http:InternalServerError|http:NotFound {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        database:MicroApp|error? microApp = database:getMicroAppById(appId, userInfo.groups ?: []);

        if microApp is error {
            string customError = "Error occurred while retrieving the Micro App for the given app ID!";
            log:printError(customError, microApp);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        if microApp is () {
            string customError = "Micro App not found for the given app ID!";
            log:printError(customError, appId = appId);
            return <http:NotFound>{
                body: {
                    message: customError
                }
            };
        }

        return microApp;
    }

    # Retrieves Super App version details for a given platform.
    #
    # + ctx - Request context
    # + platform - Target platform to fetch versions for (android or ios)
    # + return - A list of database:Version records if successful, or an error on failure
    resource function get versions(http:RequestContext ctx, string platform)
        returns database:Version[]|http:InternalServerError {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        database:Version[]|error versions = database:getVersionsByPlatform(platform);
        if versions is error {
            string customError = "Error occurred while retrieving versions for the given platform!";
            log:printError(customError, versions);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return versions;
    }

    # Fetch the user configurations(downloaded microapps) of the logged in user.
    #
    # + ctx - Request context
    # + return - User configurations or error
    resource function get users/user\-configs(http:RequestContext ctx)
        returns database:UserConfig[]|http:InternalServerError {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return {
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        database:UserConfig[]|error userConfigs = database:getUserConfigs(userInfo.userId);
        if userConfigs is error {
            string customError = "Error occurred while retrieving app configurations for the user!";
            log:printError(customError, userConfigs);
            return {
                body: {
                    message: customError
                }
            };
        }
        log:printDebug("Fetched user configurations...", userId = userInfo.userId, configs = userConfigs);
        return userConfigs;
    }

    # Add/Update user configurations(downloaded microapps) of the logged in user.
    #
    # + ctx - Request context
    # + configuration - User's user configurations including downloaded microapps
    # + return - Created response or error
    resource function post users/user\-configs(http:RequestContext ctx,
            database:UserConfig configuration) returns http:Created|http:InternalServerError|http:BadRequest {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        log:printDebug("Updating user configurations...", userId = userInfo.userId, configs = configuration);
        database:ExecutionSuccessResult|error result =
            database:updateUserConfigs(userInfo.userId, configuration);
        if result is error {
            string customError = "Error occurred while updating the user configuration!";
            log:printError(customError, result);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return http:CREATED;
    }

    # Retrieves FCM tokens for all members of a specified group.
    #
    # + ctx - Request context
    # + group - The group name to search for members
    # + startIndex - Starting index for pagination
    # + return - Paginated FCM tokens response or an error
    resource function get users/fcm\-tokens(http:RequestContext ctx, string group, int startIndex)
        returns database:FcmTokenResponse|http:InternalServerError|http:NotFound {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {message: ERR_MSG_USER_HEADER_NOT_FOUND}
            };
        }

        string[]|error memberIds = scim:getGroupMemberIds(group);
        if memberIds is error {
            string customError = "Error occurred while calling SCIM operations service";
            log:printError(customError, memberIds);
            return <http:InternalServerError>{
                body: {message: customError}
            };
        }
        if memberIds.length() == 0 {
            string customError = string `No members found in the requested group or the group does not exist.`;
            return <http:NotFound>{
                body: {message: customError}
            };
        }

        database:FcmTokenResponse|error fcmTokensResponse = database:getFcmTokens(memberIds, startIndex);
        if fcmTokensResponse is error {
            string customError = "Error occurred while retrieving FCM tokens";
            log:printError(customError, fcmTokensResponse);
            return <http:InternalServerError>{
                body: {message: customError}
            };
        }

        return fcmTokensResponse;
    }

    # Adds a new FCM token.
    #
    # + ctx - Request context
    # + fcmToken - The FCM token to be stored
    # + return - `http:Ok` on success with a confirmation message, or `http:InternalServerError` if the operation fails
    resource function post users/fcm\-tokens(http:RequestContext ctx, string fcmToken)
        returns http:Ok|http:InternalServerError {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        log:printDebug("Adding FCM token...", userId = userInfo.userId, fcmToken = fcmToken);
        database:ExecutionSuccessResult|error result = database:addFcmToken(userInfo.userId, fcmToken);
        if result is error {
            string customError = "Error occurred while adding FCM token";
            log:printError(customError, result);
            return <http:InternalServerError>{
                body: {message: customError}
            };
        }

        return <http:Ok>{body: {message: result}};
    }

    # Search for FCM tokens using user UUIDs.
    #
    # + ctx - Request context
    # + request - Request containing userIds and startIndex
    # + return - Paginated FCM tokens response or an error
    resource function post fcm\-tokens/search(http:RequestContext ctx, database:FcmTokenRequest request)
        returns http:InternalServerError|http:BadRequest|http:Ok {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {message: ERR_MSG_USER_HEADER_NOT_FOUND}
            };
        }

        if request.startIndex < 1 || request.itemsPerPage <= 0 || request.itemsPerPage > MAX_ITEMS_PER_PAGE {
            return <http:BadRequest>{
                body: {message: string`'startIndex' must be >= 1 and 'itemsPerPage' must be > 0 and <= ${MAX_ITEMS_PER_PAGE}`}
            };
        }

        string[] emails = request.emails;
        if emails.length() == 0 {
            return <http:BadRequest>{
                body: {message: "emails array cannot be empty"}
            };
        }

        string[]|error userIds = scim:getUserIdsByEmails(emails);
        if userIds is error {
            string customError = "Error occurred while calling SCIM operations service";
            log:printError(customError, userIds);
            return <http:InternalServerError>{
                body: {message: customError}
            };
        }
        if userIds.length() == 0 {
            return <http:Ok>{
                body: {
                    fcmTokens: [],
                    totalResults: 0,
                    startIndex: request.startIndex,
                    itemsPerPage: 0
                }
            };
        }

        database:FcmTokenResponse|error fcmTokensResponse = 
            database:getFcmTokens(userIds, request.startIndex - 1, request.itemsPerPage);
        if fcmTokensResponse is error {
            string customError = "Error occurred while retrieving FCM tokens";
            log:printError(customError, fcmTokensResponse);
            return <http:InternalServerError> {
                body: {message: customError}
            };
        }

        return <http:Ok> {
            body: {
                fcmTokens: fcmTokensResponse.fcmTokens,
                totalResults: fcmTokensResponse.totalResults,
                startIndex: fcmTokensResponse.startIndex,
                itemsPerPage: fcmTokensResponse.itemsPerPage
            }
        };
    }

    # Deletes the specified FCM token.
    #
    # + ctx - Request context
    # + fcmToken - The FCM token to be deleted
    # + return - `http:Ok` on success with a confirmation message, or `http:InternalServerError` if the deletion fails
    resource function delete users/fcm\-tokens(http:RequestContext ctx, string fcmToken)
        returns http:Ok|http:InternalServerError {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        database:ExecutionSuccessResult|error result = database:deleteFcmToken(fcmToken);
        if result is error {
            string customError = "Error occurred while deleting FCM token";
            log:printError(customError, result);
            return <http:InternalServerError>{
                body: {message: customError}
            };
        }
        return <http:Ok>{body: {message: result}};
    }

    # Retrieves a list of notifications filtered by the user's groups.
    #
    # + ctx - Request context
    # + startIndex - Start index for pagination
    # + itemsPerPage - Items per page
    # + return - List of notifications or http:InternalServerError
    resource function get user/notifications(http:RequestContext ctx, int startIndex,
            int itemsPerPage = NOTIFICATION_ITEMS_PER_PAGE)
        returns database:NotificationResponse|http:InternalServerError|http:BadRequest {

        authorization:CustomJwtPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_HEADER_NOT_FOUND
                }
            };
        }

        string[] groups = userInfo.groups ?: [];

        database:NotificationResponse|error? notifications =
            database:getNotifications(groups, userInfo.userId, startIndex, itemsPerPage);

        if notifications is () {
            return {
                notifications: [],
                totalResults: 0,
                startIndex: 0,
                itemsPerPage: 0
            };
        }

        if notifications is error {
            string customError = "Error occurred while retrieving notifications";
            log:printError(customError, notifications);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return notifications;
    }
}
